buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'https://maven.ampthedev.club' // MCGradle
        }
        maven {
            url 'https://files.minecraftforge.net/maven' // MCGradle uses some Forge libraries
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.61"
        classpath 'tk.amplifiable.mcgradle:MCGradle:1.2.6'
    }
}

import tk.amplifiable.gitversion.GitVersion

apply plugin: 'tk.amplifiable.mcgradle'

mcgradle {
    version '1.8.9'
    hyperiumVersion = GitVersion.gitVersion(getRootDir())
    kotlinVersion '1.3.61'
    clientMainClass = 'net.minecraft.launchwrapper.Launch'
}

group 'cc.hyperium'
version mcgradle.hyperiumVersion

subprojects {
    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'tk.amplifiable.mcgradle'
    apply plugin: 'club.ampthedev.versionjson'

    configurations {
        embed
        compile.extendsFrom(embed)
    }

    repositories {
        mavenCentral()
        maven {
            url 'https://maven.ampthedev.club'
        }
        maven {
            url 'https://jitpack.io'
        }
        maven {
            url 'https://libraries.minecraft.net'
        }
        maven {
            url 'https://repo.spongepowered.org/maven'
        }
    }

    configurations {
        embed
        compile.extendsFrom embed
    }

    dependencies {
        library "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.61"
        library "org.jetbrains.kotlin:kotlin-reflect:1.3.61"

        library 'com.github.jagrosh:DiscordIPC:e29d6d8'

        library 'com.github.FalseHonesty:reflections:855f40b80b'
        library 'com.github.ben-manes.caffeine:caffeine:2.8.0'

        library('org.slick2d:slick2d-core:1.0.2') {
            exclude module: 'lwjgl'
        }

        library("com.github.KevinPriv:MojangAPI:1.0") {
            exclude module: "gson"
        }

        library 'com.github.Cubxity:DeobfStack:1.2'

        library 'org.ow2.asm:asm:5.0.3'

        library 'net.minecraft:launchwrapper:1.12' // still need this for addons

        embed rootProject.fileTree(dir: 'libs', include: '*.jar')
        library('org.spongepowered:mixin:0.7.11-SNAPSHOT') { // we still need this for addons
            exclude module: 'launchwrapper'
            exclude module: 'guava'
            exclude module: 'gson'
            exclude module: 'commons-io'
        }

        // patching
        library group: 'com.nothome', name: 'javaxdelta', version: '2.0.1'
        library 'com.github.jponge:lzma-java:1.3'
        // conflicts
        library('com.github.half-cambodian-hacker-man:Koffee:d8cee73') {
            exclude module: 'asm-commons'
            exclude module: 'asm-tree'
            exclude module: 'asm'
        }
    }

    jar {
        dependsOn configurations.compile
        from {
            configurations.embed.collect {
                it.isDirectory() ? it : zipTree(it)
            }
        }
        exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
    }
}