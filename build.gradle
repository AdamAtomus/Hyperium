buildscript {
    repositories {
        maven { url 'https://jitpack.io' }
        maven { url 'https://files.minecraftforge.net/maven' }
        mavenCentral()
    }
    dependencies {
        // MCGradle 2.0.10
        classpath 'com.github.asbyth:MCGradle:09f84c1c42906c19e0f7902512a9215481279b4e'
    }
}

plugins {
    id 'club.ampthedev.versionjson'
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'maven-publish'
}

def kotlinVersion = '1.3.72'

apply plugin: 'club.ampthedev.mcgradle'

//noinspection GroovyUnusedAssignment
sourceCompatibility = targetCompatibility = '1.8'
compileJava.options.encoding = 'UTF-8'

mcgradle {
    version '1.8.9'
    gitVersion = true
    clientMainClass = 'net.minecraft.launchwrapper.Launch'
    args += ['--tweakClass', 'cc.hyperium.launch.HyperiumTweaker']
}

group 'cc.hyperium'
mcgradle.hyperiumVersion = project.version

configurations {
    embed
    compile.extendsFrom(embed)
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url 'https://libraries.minecraft.net' }
    maven { url 'https://repo.spongepowered.org/maven' }
}

// Jitpack compatibility
if (!new File(projectDir, 'src/minecraft').exists()) {
    tasks.compileKotlin.dependsOn tasks.setupCIWorkspace
    tasks.compileJava.dependsOn tasks.setupCIWorkspace
}

dependencies {
    library "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    library "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"

    library 'com.github.jagrosh:DiscordIPC:e29d6d8'

    library 'com.github.FalseHonesty:reflections:855f40b80b'
    library 'com.github.ben-manes.caffeine:caffeine:2.8.0'

    library('org.slick2d:slick2d-core:1.0.2') {
        exclude module: 'lwjgl'
    }

    library("com.github.KevinPriv:MojangAPI:1.0") {
        exclude module: "gson"
    }

    library 'org.ow2.asm:asm:5.0.3'

    library('net.minecraft:launchwrapper:1.12') { // still need this for addons
        exclude module: 'lwjgl'
    }

    embed rootProject.fileTree(dir: 'libs', include: '*.jar')
    library('org.spongepowered:mixin:0.7.11-SNAPSHOT') { // we still need this for addons
        exclude module: 'launchwrapper'
        exclude module: 'guava'
        exclude module: 'gson'
        exclude module: 'commons-io'
    }

    // patching
    library group: 'com.nothome', name: 'javaxdelta', version: '2.0.1'
    library 'com.github.jponge:lzma-java:1.3'
    // conflicts
    library('com.github.half-cambodian-hacker-man:Koffee:d8cee73') {
        exclude module: 'asm-commons'
        exclude module: 'asm-tree'
        exclude module: 'asm'
    }
}

jar {
    dependsOn configurations.compile
    from {
        configurations.embed.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
}

publishing {
    repositories {
        maven {
            url = "$buildDir/repository"
        }
    }

    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'Hyperium'
                description = 'Open Source 1.8.9 Minecraft Client'
                url = 'https://github.com/HyperiumClient/Hyperium'
                licenses {
                    license {
                        name = 'GNU LGPL-3.0'
                        url = 'https://raw.githubusercontent.com/HyperiumClient/Hyperium/master/LICENSE'
                    }
                }

                developers {
                    developer {
                        id = 'hyperium'
                        name = 'asbyth'
                        email = 'asbyth@sk1er.club'
                    }
                }
            }
        }
    }
}