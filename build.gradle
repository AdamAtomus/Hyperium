buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'https://maven.amplifiable.tk' // MCGradle
        }
        maven {
            url 'https://files.minecraftforge.net/maven' // MCGradle uses some Forge libraries
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.61"
        classpath 'tk.amplifiable.mcgradle:MCGradle:1.2.2'
    }
}

import tk.amplifiable.gitversion.GitVersion

apply plugin: 'tk.amplifiable.mcgradle'

mcgradle {
    version '1.8.9'
    hyperiumVersion = GitVersion.gitVersion(getRootDir())
    kotlinVersion '1.3.61'
    clientMainClass = 'net.minecraft.launchwrapper.Launch'
}

group 'cc.hyperium'
version mcgradle.hyperiumVersion

subprojects {
    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'tk.amplifiable.mcgradle'

    configurations {
        embed
        compile.extendsFrom(embed)
    }

    repositories {
        mavenCentral()
        maven {
            url 'https://maven.amplifiable.tk'
        }
        maven {
            url 'https://jitpack.io'
        }
        maven {
            url 'https://libraries.minecraft.net'
        }
        maven {
            url 'https://repo.spongepowered.org/maven'
        }
    }

    configurations {
        embed
        compile.extendsFrom embed
    }

    dependencies {
        embed "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.61"
        embed "org.jetbrains.kotlin:kotlin-reflect:1.3.61"

        embed 'com.github.jagrosh:DiscordIPC:e29d6d8'

        embed 'com.github.FalseHonesty:reflections:855f40b80b'
        embed 'com.github.ben-manes.caffeine:caffeine:2.8.0'

        embed('org.slick2d:slick2d-core:1.0.2') {
            exclude module: 'lwjgl'
        }

        embed("com.github.KevinPriv:MojangAPI:1.0") {
            exclude module: "gson"
        }

        embed 'com.github.Cubxity:DeobfStack:1.2'

        embed 'org.ow2.asm:asm:5.0.3'

        compile 'net.minecraft:launchwrapper:1.12' // still need this for addons

        embed rootProject.fileTree(dir: 'libs', include: '*.jar')
        embed('org.spongepowered:mixin:0.7.11-SNAPSHOT') { // we still need this for addons
            exclude module: 'launchwrapper'
            exclude module: 'guava'
            exclude module: 'gson'
            exclude module: 'commons-io'
        }

        // patching
        embed group: 'com.nothome', name: 'javaxdelta', version: '2.0.1'
        embed 'com.github.jponge:lzma-java:1.3'
        // conflicts
        embed 'com.github.half-cambodian-hacker-man:Koffee:2e8004f'
    }

    jar {
        dependsOn configurations.compile
        from {
            configurations.embed.collect {
                it.isDirectory() ? it : zipTree(it)
            }
        }
        exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
    }
}