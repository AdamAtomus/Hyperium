buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'https://maven.amplifiable.tk' // MCGradle
        }
        maven {
            url 'https://files.minecraftforge.net/maven' // MCGradle uses some Forge libraries
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.60"
        classpath 'tk.amplifiable.mcgradle:MCGradle:1.2.1'
    }
}

import tk.amplifiable.gitversion.GitVersion

apply plugin: 'tk.amplifiable.mcgradle'

mcgradle {
    version '1.8.9'
    hyperiumVersion = GitVersion.gitVersion(getRootDir())
    kotlinVersion '1.3.60'
    clientMainClass = 'net.minecraft.launchwrapper.Launch'
}

group 'cc.hyperium'
version mcgradle.hyperiumVersion

subprojects {
    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'tk.amplifiable.mcgradle'

    configurations {
        embed
        compile.extendsFrom(embed)
    }

    repositories {
        mavenCentral()
        maven {
            url 'https://maven.amplifiable.tk'
        }
        maven {
            url 'https://jitpack.io'
        }
        maven {
            url 'https://libraries.minecraft.net'
        }
        maven {
            url 'https://repo.spongepowered.org/maven'
        }
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.60"
        compile "org.jetbrains.kotlin:kotlin-reflect:1.3.60"

        compile 'com.github.jagrosh:DiscordIPC:e29d6d8'

        compile 'com.github.FalseHonesty:reflections:855f40b80b'
        compile 'com.github.ben-manes.caffeine:caffeine:2.8.0'

        compile('org.slick2d:slick2d-core:1.0.2') {
            exclude module: 'lwjgl'
        }

        compile("com.github.KevinPriv:MojangAPI:1.0") {
            exclude module: "gson"
        }

        compile 'com.github.Cubxity:DeobfStack:1.2'

        compile 'org.ow2.asm:asm:5.0.3'

        compile 'net.minecraft:launchwrapper:1.12' // still need this for addons

        embed rootProject.fileTree(dir: 'libs', include: '*.jar')
        embed('org.spongepowered:mixin:0.7.11-SNAPSHOT') { // we still need this for addons
            exclude module: 'launchwrapper'
            exclude module: 'guava'
            exclude module: 'gson'
            exclude module: 'commons-io'
        }
    }
}